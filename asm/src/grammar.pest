program = _{ SOI ~ "\n"* ~ (line ~ "\n"+)* ~ line? ~ EOI }

stmt = {
    quaternary_stmt
    | ternary_stmt
    | binary_stmt
    | unary_stmt
    | data
    | data_array
    | halt
    | ret
}

line = _{ label? ~ (stmt ~ ";"+)* ~ stmt? }
label = ${ ident ~ ":" }
halt = { "hlt" | "halt" }
ret = { "ret" }
unary_stmt = { unary_instr ~ param }
binary_stmt = { binary_instr ~ param ~ param }
ternary_stmt = { ternary_instr ~ param ~ param ~ param }
quaternary_stmt = { quaternary_instr ~ param ~ param ~ param ~ param }
data = { "data" ~ param+ }
data_array = { "array" ~ num ~ num }

unary_instr = { "in" | "out" | "jmp" | "push" | "pop" | "call" | "add_rel_base" }
binary_instr = { "jnz" | "jmp_true" | "jz" | "jmp_false" | "not" | "mov" | "load" | "store" }
ternary_instr = { "add" | "mul" | "sub" | "div" | "mod" | "eq" | "neq" | "lt" | "leq" | "gt" | "geq" | "and" | "or" }
quaternary_instr = { "divmod" }

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
ident_immediate = ${ ":" ~ ident }
ident_relative = ${ "%" ~ ident }
num = ${ decimal | hex | oct | bin }
num_positional = { "[" ~ num ~ "]" }
num_relative = ${ "%" ~ num }
decimal = @{ "-"? ~ ASCII_DIGIT+ }
hex = @{ "-"? ~ "0x" ~ ASCII_HEX_DIGIT+ }
oct = @{ "-"? ~ "0o" ~ ASCII_OCT_DIGIT+ }
bin = @{ "-"? ~ "0b" ~ ASCII_BIN_DIGIT+ }
param = { ident | ident_immediate | ident_relative | num | num_positional | num_relative }

WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }
